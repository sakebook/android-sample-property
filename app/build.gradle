def str = parent.hasProperty("str") ? parent.property("str") : ""
def uint = parent.hasProperty("uint") ? parent.property("uint") : 0
def bool = parent.hasProperty("bool")

apply plugin: 'com.android.application'

android {
    compileSdkVersion 21
    buildToolsVersion "21.1.2"

    defaultConfig {
        applicationId "com.sakebook.android.sample.propertysample"
        minSdkVersion 15
        targetSdkVersion 21
        versionCode 1
        versionName "1.0"
    }

    signingConfigs {
        release {
            storeFile file("../debug.keystore")
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }
    }

    productFlavors {
        production {
            buildConfigField "String", "STR", '"'+str+'"'
            buildConfigField "int", "UINT", String.valueOf(uint)
            buildConfigField "boolean", "BOOL", String.valueOf(bool)
        }
        log {
            buildConfigField "String", "STR", '"'+str+'"'
            buildConfigField "int", "UINT", String.valueOf(uint)
            buildConfigField "boolean", "BOOL", String.valueOf(bool)
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            applicationIdSuffix '.dev'
        }
    }

    applicationVariants.all() {
        def output = outputs.get(0).outputFile
        println output.dump()
        if (buildType.name.equals("release") && flavorName.equals("production")) {
            outputs.get(0).outputFile = new File(output.parentFile, getProdApkName())
        }
        if (buildType.name.equals("debug") && flavorName.equals("log")) {
            outputs.get(0).outputFile = new File(output.parentFile, getDevApkName())
        }
    }
}

def getDevApkName() {
    return android.defaultConfig.applicationId + android.buildTypes.debug.applicationIdSuffix + ".apk";
}

def getProdApkName() {
    return rootProject.name + "-" + android.defaultConfig.versionName + "-" + android.defaultConfig.versionCode + ".apk";
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:21.0.3'
}